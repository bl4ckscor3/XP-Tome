plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.+'
}

version = '2.4'
group = 'bl4ckscor3.mod.xptome'

base {
	archivesName = 'xptome-1.20.6'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
	exclude ".cache"
}

sourceSets.main.resources {
	srcDir 'src/generated/resources' //include generated files
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        workingDirectory project.file('runs/client')
    }

    server {
        workingDirectory project.file('runs/server')
        argument '-nogui'
    }

    data {
		workingDirectory project.file('runs/data')
		arguments.addAll '--mod', 'xpbook', '--all', '--output', file('src/generated/resources/').getAbsolutePath()
	}
}

dependencies {
    implementation 'net.neoforged:neoforge:20.6.119'
}

tasks.named('jar', Jar).configure {
	exclude('bl4ckscor3/mod/xptome/datagen/**') //exclude files from the built jar that are only used to generate the assets & data

    manifest {
        attributes(["Specification-Title": "XP Tome",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "XP Tome",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3"])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
	options.release = 21
}